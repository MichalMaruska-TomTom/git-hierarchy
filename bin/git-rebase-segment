#! /usr/bin/zsh -feu

# rebase  start...segment --onto base

source /usr/share/git-hierarchy/functions.sh
GIT_DIR=$(git_dir)
unsetopt FUNCTION_ARGZERO

DEBUG=n
SILENT=n

usage()
{
    cat <<EOF
usage: ${0##*/} [-qv] [--] ARGS...

-v verbose
-q quiet
EOF
}

while getopts :hqv OPT; do
    case $OPT in
	h)
	    usage
	    exit 0
	    ;;
	q)
	    SILENT=y
	    ;;
	v)
	    DEBUG=y
	    ;;
	*)
	    usage
	    exit 2
    esac
done
shift OPTIND-1
OPTIND=1


# fixme: canonize!
branch=${1#refs/heads}

# not needed "ref/
start="refs/start/$branch"
base="refs/base/$branch"

if ! git-segment $branch >/dev/null ;
then
    exit 1
fi

commit_id()
{
    #local sha=$(git rev-list  --max-count=1 $commit)
    git rev-parse $1
}


if test $(commit_id $start) = $(commit_id $base);
then
    test $SILENT = n && \
	{echo -n "no need to rebase segment ";cecho cyan $branch} >&2
    exit 0
fi

{cecho yellow -n "rebasing segment ";cecho cyan $branch} >&2
# This might fail. in that case:
# 1/ save the state, to reinvoke later:

check_git_rebase_hooks

if git rebase --onto $base $start $branch;
then
    # todo: git-segment $branch $base
    git-set-start $branch $base
else
    cecho hiyellow $(cat $GIT_DIR/rebase-apply/final-commit)


    echo "$branch" > $GIT_DIR/.rebasing-segment
    cecho red "** ERROR: Rebase failed."
    echo "Finish the rebase manually (resolve conflicts, and git rebase --continue or --skip"
    cecho green "then run:"
    echo -e "\tgit-reset-segment $branch"
    exit 1
fi

