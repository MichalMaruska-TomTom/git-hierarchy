#! /usr/bin/zsh -feu

# rebase  (start...segment) --onto base
#
source /usr/share/git-hierarchy/functions.sh
GIT_DIR=$(git_dir)
if cherry_pick_in_progress;then
   cecho red "cherry-pick in progress!" >&2
   exit 1
fi

# necessary?
unsetopt FUNCTION_ARGZERO
SILENT=n
onto=""

usage()
{
    cat <<EOF
usage: ${0##*/} [-qv] [-o onto] [--] [branch]
-o onto some commit, by default onto its BASE.
-v verbose
-q quiet
-s stash-if-needed

-n dry
branch ... segment-name, by default the current branch.
EOF
}

dry=n
while getopts :hno:qsv OPT; do
    case $OPT in
        h)
            usage
            exit 0
            ;;
        s)
            stash_if_needed=y
            ;;
        n)
            dry=y
            ;;
        o)
            onto=$OPTARG
            ;;
        q)
            SILENT=y
            ;;
        v)
            SILENT=n
            ;;
        *)
            echo "unexpected option $OPT">&2
            usage >&2
            exit 2
    esac
done
shift OPTIND-1
OPTIND=1

#fixme:
if [ $# = 0 ]; then
    # default is the current branch
    branch=$(current_branch_name)
elif [ $# = 1 ]; then
    branch=${1}
fi

cd $(git rev-parse --show-toplevel);
branch=${branch#refs/heads/}

if ! git-segment $branch >/dev/null ;
then
    echo "this is not a segment" >&2
    exit 1
fi


# not needed "ref/
start="refs/start/$branch"
if [ -z $onto ]; then
   # the base
   onto=$(segment_base_name $branch)
fi

if [[ $dry = y ]];
then
    if [[ $(git rev-parse $start) = $(git rev-parse $onto) ]];
    then
        exit 0
    else
        echo "should rebase segment $branch" >&2
        exit 0
    fi
fi

### STASH:
mmc_stash_if_non_clean "git-rebase-segment saving"

rebase_empty_segment()
{
    local -r name=$1
    local -r base=$2
    cecho yellow "rebase_empty_segment $name"

    if [[ $name = $(current_branch_name) ]]
    then
        git reset --hard $base
    else
        git branch --force $name $base
    fi
    git-reset-segment $name
}

if test $(commit_id $start) = $(commit_id $onto);
then
    test $SILENT = n && \
        {echo -n "no need to rebase segment ";cecho cyan $branch} >&2
    exit 0
elif test $(commit_id $start) = $(commit_id $branch);
then
    rebase_empty_segment $branch $onto
    exit 0
fi

{cecho yellow -n "rebasing segment ";cecho cyan $branch} >&2

# This might fail. in that case:
# 1/ save the state, to reinvoke later:

# For now I can do without the hooks. At least in the succesful case.
export GIT_USE_COLOR=y

# fixme: this establishes a binding, and one cannot --delete w/o merging into $onto.
#
if git checkout --no-track -B temp $onto &&
   git cherry-pick $start..$branch

## the alternative way:
# git rebase --onto $onto $start $branch;
then
    exit_status=0
    git-done-segment $branch "temp"
else
    exit_status=1
    mark_rebase_segment $branch
fi

mmc_unstash_if_stashed

exit $exit_status
