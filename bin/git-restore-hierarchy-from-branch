#!/bin/zsh -feu

zmodload zsh/regex

source /usr/share/mmc-shell/git-functions.sh
source /usr/share/mmc-shell/mmc-functions.sh
colors
source /usr/share/git-hierarchy/functions.sh

hierarchy_branch="_hierarchy"

usage()
{
    cat <<EOF
usage: ${0##*/} [+-hn]  [-r remote] [--] ARGS...

-n   ... dump (only) the last hierarchy
-r remote  ... instead of local branch $hierarchy_branch, use the the remote one.

-l list all remote "$hierarchy_branch" branches

******************
Create segments/sums following hierarchy on $hierarchy_branch

EOF
}



# todo: share with ./git-store-hierarchy
FILENAME=hierarchy
REMOTE="heads"

# uses $REMOTE
reconstruct_segment()
{
    readonly segment=$1
    readonly base=$2

    if is_segment $segment; then
        cecho red "we already have segment $segment"
    else
        if git rev-parse $base;
        then
            git-segment $segment $base $base remotes/$REMOTE/$segment
        else
            cecho red "don't have the base of segment $segment: $base"
        fi
    fi
}

reconstruct_sum()
{
    readonly sum=$1
    shift
    readonly summands=($@)
    # check all the summands exist

    (set -x
    git-sum $sum $summands
    )
}

cat_only=n
while getopts :hlnr: OPT; do
    case $OPT in
        h|+h)
            usage
            exit 0
            ;;
        r)
            REMOTE=$OPTARG
            # optiplex
            ;;
        l)
            echo "let's find possible branches:"
            foreach remote ( $(git remote ) ) {
                if git rev-parse remotes/$remote/$hierarchy_branch >/dev/null;
                then
                    echo "$remote/$hierarchy_branch !"
                fi
            }
            # git rev-parse   'remotes/origin/_hierarchy'
            exit 0
            ;;
        n|+n)
            cat_only=y
            ;;
        *)
            usage>&2
            exit 2
    esac
done
shift OPTIND-1
OPTIND=1


if [[ $cat_only = y ]]; then
    # from mmc-shell
    cat_git_file_from_commit $REMOTE/$hierarchy_branch $FILENAME
    exit
fi

# Maybe this should invoke a tool which `only' creates?
cat_git_file_from_commit $REMOTE/$hierarchy_branch $FILENAME | \
    while read line; do
        echo ">$line<"
        if [[ $line -regex-match '^segment ([[:graph:]]+)[[:space:]]+([[:graph:]]+)$' ]]
        then
            # variables:
            segment=$match[1]
            base=$match[2]
            reconstruct_segment $segment $base

        elif [[ $line -regex-match '^sum[[:space:]]+([[:graph:]]+)$' ]]
        then
             sum=$match[1]
             # read the summands:
             summands=()

             while read line; do
                 if [[ $line = '' ]]; then
                     # empty line = end.
                     break;
                 fi
                 summands+=($line)
             done

             reconstruct_sum $sum $summands
        fi
    done
